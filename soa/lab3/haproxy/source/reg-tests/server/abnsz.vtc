varnishtest "Abstract unix socket - zero terminated"
feature ignore_unknown_macro
feature cmd "command -v curl"

# abns@ sockets are not available on freebsd
#EXCLUDE_TARGETS=freebsd,osx,generic
#REGTEST_TYPE=devel

haproxy h1 -Ws -S -conf {
  global
    stats socket "${tmpdir}/h1/stats" level admin expose-fd listeners

  defaults
    mode http
    timeout connect "${HAPROXY_TEST_TIMEOUT-5s}"
    timeout client  "${HAPROXY_TEST_TIMEOUT-5s}"
    timeout server  "${HAPROXY_TEST_TIMEOUT-5s}"

  listen testme
    bind "fd@${testme}"
    server f2 abnsz@hap-f2

  frontend f2
    bind abnsz@hap-f2
    http-request return status 200 content-type text/plain string "ok"
} -start

client c1 -connect ${h1_testme_sock} {
    txreq -url "/"
    rxresp
} -run

shell {
    curl -sfS --abstract-unix-socket hap-f2 "http://host/" | grep "ok"
}
